# fetch a specific nixpkgs revision as a package stored in the nix store
# referenced under an output hash to have faster evaluations and allow keeping
# it alive when garbage collecting as a package dependency.
let
    # so that we can use `runCommand`
    bootStrap = import <nixpkgs> {};
    # the revision of the nixpkgs repo we are pegging to
    rev = "72a2ced252348defc877bea0a8b551272c0be3f9";
    # hash generated by doing nix-prefetch-url --unpack https://github.com/NixOS/nixpkgs-channels/archive/{rev}.tar.gz
    hash = "0vxqx5zsc8blzny89pwmhap4l105ryhpay1xviyxvcn7hx93i39x";
    src = builtins.fetchTarball
    {
      name = "nixpkgs-tarball-${rev}";
      url = "https://github.com/NixOS/nixpkgs-channels/archive/${rev}.tar.gz";
      sha256 = hash;
    };
in
   bootStrap.runCommand "nixpkgs-${rev}" {
    outputHashMode = "recursive";
    outputHashAlgo = "sha256";
    outputHash = hash;
    inherit src;
  }
  ''
  cp -r $src $out
  ''

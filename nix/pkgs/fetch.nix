# fetch a specific nixpkgs revision as a package stored in the nix store
# referenced under an output hash to have faster evaluations and allow keeping
# it alive when garbage collecting as a package dependency.
let
    # so that we can use `runCommand`
    bootStrap = import <nixpkgs> {};
    # the revision of the nixpkgs repo we are pegging to
    rev = "c75de8bc12cc7e713206199e5ca30b224e295041";
    # hash generated by doing nix-prefetch-url --unpack https://github.com/NixOS/nixpkgs-channels/archive/{rev}.tar.gz
    hash = "1awipcjfvs354spzj2la1nzmi9rh2ci2mdapzf4kkabf58ilra6x";
    src = builtins.fetchTarball
    {
      name = "nixpkgs-tarball-${rev}";
      url = "https://github.com/NixOS/nixpkgs-channels/archive/${rev}.tar.gz";
      sha256 = hash;
    };
in
   bootStrap.runCommand "nixpkgs-${rev}" {
    outputHashMode = "recursive";
    outputHashAlgo = "sha256";
    outputHash = hash;
    inherit src;
  }
  ''
  cp -r $src $out
  ''
